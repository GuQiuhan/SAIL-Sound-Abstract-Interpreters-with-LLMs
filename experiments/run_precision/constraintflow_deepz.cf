def Shape as (Float l, Float u, SymExp Z){[(curr[u]>=curr),(curr In curr[Z]),(curr[l]<=curr)]};

func simplify_lower(Neuron n, Float coeff) = (coeff >= 0) ? (coeff * n[l]) : (coeff * n[u]);
func simplify_upper(Neuron n, Float coeff) = (coeff >= 0) ? (coeff * n[u]) : (coeff * n[l]);

func priority(Neuron n) = n[layer];
func f(Neuron n1, Neuron n2) = n1[l] >= n2[u];

func s1(Float x1, Float x2) = ((x1 * (x1 + 3))-(x2 * (x2 + 3))) / (6 * (x1-x2));
func i1(Float x1, Float x2) = x1 * ((x1 + 3) / 6) - (s1(x1, x2) * x1);

func f1(Float x) = x < 3 ? x * ((x + 3) / 6) : x;
func f2(Float x) = x * ((x + 3) / 6);

transformer deepZ{
    Affine -> ((prev.dot(curr[weight]) + curr[bias]).map(simplify_lower), (prev.dot(curr[weight]) + curr[bias]).map(simplify_upper), prev[Z].dot(curr[weight]) + (curr[bias]));

    Relu -> ((prev[l]) >= 0) ? ((prev[l]), (prev[u]), (prev[Z])) : (((prev[u]) <= 0) ? (0, 0, 0) : (0, (prev[u]), ((prev[u]) / 2) + (((prev[u]) / 2) * eps)));
    Abs -> ((prev[l]) >= 0) ?
                ((prev[l]), (prev[u]), (prev[Z])) :
                (((prev[u]) <= 0) ?
                    (-(prev[u]), -(prev[l]), -(prev[Z])) :
                    (0, max(-prev[l], prev[u]), ((max(-prev[l], prev[u])) / 2) + (((max(-prev[l], prev[u])) / 2) * eps)));
    HardSwish -> (prev[l] < -3) ?
                    (prev[u] < -3 ?
                        (0, 0, 0) :
                        (prev[u] < 0 ?
                            (-3/8, 0, (-3/16) * (1 - eps)) :
                            (-3/8, f1(prev[u]), (f1(prev[u])/2 - (3/16)) + ((f1(prev[u])/2 + (3/16)) * eps) ))) :
                    ((prev[l] < 3) ?
                        ((prev[u] < 3) ?
                            (-3/8, max(f2(prev[l]), f2(prev[u])), ((max(f2(prev[l]), f2(prev[u]))/2 )- (3/16)) + (eps * (max(f2(prev[l]), f2(prev[u]))/2 + (3/16)))) :
                            (-3/8, prev[u], (prev[u]/2 - (3/16)) + (eps * (prev[u]/2 + (3/16))) )) :
                        (prev[l], prev[u], prev[Z]));

    Sigmoid -> ((prev[l]) >= 1) ?
                    (1, 1, 1) :
                    (((prev[u]) <= -1) ?
                        (0, 0, 0) :
                        ((prev[l] >= -1) and (prev[u] <= 1) ?
                            ((prev[l] + 1)/2, (prev[u] + 1)/2, (prev[Z] + 1)/2) :
                            (prev[l] > -1 ?
                                ((prev[l] + 1)/2, 1, (prev[l] + 3)/4 + (eps * (1 - prev[l])/4) ) :
                                (prev[u] < 1 ?
                                    (0, (prev[u] + 1)/2, (1 + eps) * ((prev[u] + 1)/4)) :
                                    (0, 1, (1 + eps)/2)
                                    ))));
    Tanh -> ((prev[l]) >= 1) ?
                (1, 1, 1) :
                (((prev[u]) <= -1) ?
                    (-1, -1, -1) :
                    ((prev[l] >= -1) and (prev[u] <= 1) ?
                        (prev[l], prev[u], prev[Z]) :
                        (prev[l] > -1 ?
                            (prev[l], 1, (prev[l] + 1)/2 + (eps * (1 - prev[l])/2) ) :
                            (prev[u] < 1 ?
                                (-1, prev[u], (prev[u] - 1)/2 + (((prev[u] + 1)/2) * eps)) :
                                (-1, 1, eps)
                                ))));
}

flow(forward, priority, true, deepZ);
